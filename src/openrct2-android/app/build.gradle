apply plugin: 'com.android.application'

android {
    compileSdk 36
    ndkVersion "27.3.13750724" // Latest r27d (LTS), to be synced with CI container image
    namespace "io.openrct2"

    signingConfigs {
        release {
            if (System.getenv('OPENRCT2_KEYSTORE_FILE') && System.getenv('OPENRCT2_KEYSTORE_PASSWORD')) {
                storeFile file(System.getenv('OPENRCT2_KEYSTORE_FILE'))
                storePassword System.getenv('OPENRCT2_KEYSTORE_PASSWORD')
                keyAlias "openrct2"
                keyPassword System.getenv('OPENRCT2_KEYSTORE_PASSWORD')  // Same as keystore password in PKCS12
            }
        }
    }

    defaultConfig {
        applicationId 'io.openrct2'
        minSdkVersion 24
        targetSdkVersion 36

        versionCode 12
        versionName '0.4.25'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared', '-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON'
                targets 'openrct2', 'openrct2-ui', 'openrct2-cli'
                // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
        }
    }
    buildTypes {
        release {
            if (System.getenv('OPENRCT2_KEYSTORE_FILE') && System.getenv('OPENRCT2_KEYSTORE_PASSWORD')) {
                signingConfig signingConfigs.release
            } else {
                // Fallback to ephemeral debug signing when keystore is not available
                signingConfig signingConfigs.debug
            }
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
        java {
            srcDir 'src/main/java'
        }
    }
    externalNativeBuild {
        cmake {
            version "4.0.3"
            path 'src/main/CMakeLists.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'commons-io:commons-io:2.20.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

